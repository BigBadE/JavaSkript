plugins {
    id 'java'
    id 'idea'
    id 'io.freefair.lombok' version '5.3.0'
}

allprojects {
    group 'com.bigbade'
    version '1.0-SNAPSHOT'

    targetCompatibility = 1.16
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'

    lombok {
        version = '1.18.20'
    }

    generateLombokConfig.enabled = false

    dependencies {
        //SLF4J
        implementation 'org.slf4j:slf4j-api:1.8.0-beta4'

        //Annotations
        compileOnly 'com.google.code.findbugs:jsr305:3.+'

        //Unit Tests
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }

    configurations {
        integrationCompile.extendsFrom testImplementation
        integrationRuntime.extendsFrom testImplementation
        integrationRuntime.extendsFrom testRuntimeOnly
    }

    sourceSets {
        integrationTest {
            java.srcDir 'src/integrationTest/java'
            resources.srcDir 'src/integrationTest/resources'

            compileClasspath += main.output + test.output + configurations.integrationCompile
            runtimeClasspath += main.output + test.output + configurations.integrationRuntime
            resources.srcDir file('src/integrationTest/resources')
        }
    }

    task integrationTest(type: Test, group: 'Verification') {
        useJUnitPlatform()
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    idea {
        module {
            sourceDirs -= file('src/integrationTest/java')
            testSourceDirs += file('src/integrationTest/java')
        }
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}
