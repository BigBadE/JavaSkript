plugins {
    id 'java'
    id 'idea'
    id 'io.freefair.lombok' version '5.3.0'
}

allprojects {
    group 'com.bigbade'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'

    targetCompatibility = 1.16
    sourceCompatibility = 1.8

    lombok {
        version = '1.18.20'
    }

    generateLombokConfig.enabled = false

    dependencies {
        //SLF4J
        implementation 'org.slf4j:slf4j-api:1.8.0-beta4'

        //Annotations
        compileOnly 'com.google.code.findbugs:jsr305:3.+'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }

    configurations {
        resolveImplementation
        resolveImplementation.extendsFrom(compileOnly)
        resolveImplementation.extendsFrom(implementation)
        resolveImplementation.extendsFrom(testImplementation)
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output + configurations.resolveImplementation
                runtimeClasspath += main.output + test.output + configurations.resolveImplementation
                srcDir file('src/integrationTest/java')
            }
            resources.srcDir file('src/integrationTest/resources')
        }
    }

    task integrationTest(type: Test) {
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    idea {
        module {
            sourceDirs -= file('src/integrationTest/java')
            testSourceDirs += file('src/integrationTest/java')
        }
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}
